// Copyright 2011 Benjamin McCann. All Rights Reserved.

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.benmccann'
version = '0.1'

sourceSets {
  main {
    java { srcDir 'java' }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'com.google.javascript:closure-compiler:r1592'
  compile 'javax.servlet:javax.servlet-api:3.0.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}
 
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories.mavenDeployer {
    beforeDeployment { MavenDeployment deployment -> signPom(deployment) }
    repository(url: "http://oss.sonatype.org/service/local/staging/deploy/maven2/") {
      authentication(userName: nexusUsername, password: nexusPassword)
    }
    pom.project {
      name 'Closure Compiler Utils'
      description 'Utilities for calling the Google Closure Compiler to optimize and detect errors in JavaScript code.'
      url 'https://github.com/benmccann/closure-compiler-utils'
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
      scm {
        url 'https://github.com/benmccann/closure-compiler-utils'
        connection 'scm:git:http://github.com/benmccann/closure-compiler-utils.git'
      }
      developers {
        developer {
          id 'benmccann'
          name 'Benjamin McCann'
          organization 'BenMcCann.com'
          organizationUrl 'http://www.benmccann.com/'
        }
      }
    }
  }
}
